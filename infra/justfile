env := `../scripts/get-env.bash 2>/dev/null || true`
kubeconfig := "$HOME/.kube/configs/portfolio/" + env + "/config.yaml"
cmd := if env =~ '(production|staging)' {
    "aws-vault exec n4vysh/" + env + " -- kubie"
} else {
    "kubie"
}

init:
    ./scripts/init.bash

install:
    ./scripts/install-packages.bash

# Deploy containers to server
deploy:
    ./scripts/deploy.bash

# Destroy servers
destroy:
    ./scripts/destroy.bash

# Switch kubernetes context
switch:
    (dir=`readlink -f "{{ kubeconfig }}"` && cd "{{invocation_directory()}}" && {{cmd}} ctx -f "$dir")

# Check status of pods, nodes, and middlewares
check:
    ./scripts/check.bash

# Trigger a reconciliation of sources and resources
reconcile:
    flux reconcile kustomization flux-system --with-source

# Show highlighted access logs
log:
    ./scripts/log.bash

# Open monitoring system dashboard
viz-monitor:
    ./scripts/viz-monitor.bash

# Open service mesh dashboard
viz-nw:
    linkerd viz dashboard

# Login cloud service
login:
    aws-vault login n4vysh/{{env}}

# Get credentials
get-creds:
    ./scripts/get-creds.bash
