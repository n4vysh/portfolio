export env := `../../scripts/get-env.bash`

# Initialize
init:
    ./scripts/download-dashboards.bash

# Sync helm repositories
sync:
    helmfile -e {{ env }} repos

# Apply helm charts
apply: init sync && test
    helmfile -e {{ env }} apply

# Test helm charts
test: init sync
    helmfile -e {{ env }} test

# Render templates of helm charts
render: init sync
    helmfile -e {{ env }} template

# Show a diff explaining what a helm upgrade would change
diff: init sync
    helmfile -e {{ env }} diff

# Deletes and purges helm releases
destroy: init sync
    helmfile -e {{ env }} destroy

# Encrypt secrets
enc +file: init
    sops {{ file }}

# Decrypt secrets
dec +file: init
    sops {{ file }}
