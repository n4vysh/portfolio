user := "n4vysh"
tld := "dev"
domain := user + "." + tld
region := "ap-northeast-1"
bucket := "tfstate." + domain

export ENV := `../../scripts/get-env.bash`

default:

# Terraform is an administrative tool that manages your infrastructure, and so ideally the infrastructure that is used by Terraform should exist outside of the infrastructure that Terraform manages.
# This can be achieved by creating a separate administrative AWS account which contains the user accounts used by human operators and any infrastructure and tools used to manage the the other accounts.
# TODO: create aws account for terraform s3 backend
# https://www.terraform.io/docs/language/settings/backends/s3.html

# Setup terraform backend
setup-backend:
    aws s3api create-bucket \
        --bucket "{{bucket}}" \
        --create-bucket-configuration LocationConstraint={{region}}
    aws s3api put-bucket-versioning \
        --bucket "{{bucket}}" \
        --versioning-configuration Status=Enabled

# Build Grafana dashboard
build-dashboards:
    ./scripts/build-dashboards.bash

# Run terragrunt
run +cmd:
    terragrunt {{ cmd }} --terragrunt-working-dir=environments/$ENV/

# Run terragrunt in each sub directory
run-all +cmd:
    just run run-all {{ cmd }}

# Creates an execution plan
plan:
    just run-all plan

# Executes the planned actions
apply:
    just run-all apply

# Destroy remote objects
destroy:
    just run-all destroy
