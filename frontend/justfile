set dotenv-load := false

# Start the web server in `development`
dev:
    aleph dev

# Build a static site
build: build-icon-image build-static-site

# Install frontend packages
install:
    ./scripts/install-packages.bash

# Run frontend scripts
run *script:
    denon {{ script }}

# Build icon images
build-icon-image:
    ./scripts/build-icon-image.bash

# Build a static site
build-static-site:
    aleph build
    rsync -av dist/ ../backend/dist/

# Take a screenshot
take:
    just run screenshot

# Test performance
test:
    lhci autorun

# format js, ts, tsx, md, and json files with deno fmt
fmt-deno *target:
    deno fmt --config deno.json {{target}}

# format css file with prettier
fmt-css-prettier target='style/':
    prettier --parser css --write --list-different {{target}}

# format css files with stylelint
fmt-css-stylelint target='style/':
    stylelint --cache --fix {{target}}

# check js, ts, tsx, md, and json files with deno fmt
check-deno-fmt *target:
    deno fmt --check --config deno.json {{target}}

# check css file with prettier
check-css-prettier target='style/':
    prettier --parser css --check {{target}}

# check ts and tsx files with deno lint
lint-deno *target:
    deno lint --config deno.json {{target}}

# check css files with stylelint
lint-css target='style/':
    stylelint --cache {{target}}
